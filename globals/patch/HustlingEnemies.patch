%Hustling enemies

%Allows enemies to move multiple times
%	Even if something like zap tap interrupts their first move, they can still move again
%	Not sure if YieldTurn will break this (the answer is probably yes)



%Function for buffing enemies
%first arg = enemy ID
%second arg = additional turns (should be 1)

%you should probably wait 30 frames after this since that's how long the effect lasts
%this function also doesn't come with a message to tell what is happening

%	the effect here is just the vanish effect recolored

#export:Function:Battle $HustleTarget
{
	%copied the top of VanishActor
	PUSH		S0, S1, S2, S3, RA
	COPY      S2, A0
	LW        S1, C (S2)
	/%		??? what did this stuff do?
    BNEL      A1, R0, .o24
	SW        R0, 70 (S2)
    .o24
    LW        V0, 70 (S2)
	BNE       V0, R0, .oE0
	NOP
	%/
	LW        A1, 0 (S1)
	ADDIU     S1, S1, 4
	JAL       ~Func:get_variable
	COPY      A0, S2
	COPY      S0, V0
	LI        V0, FF81
	BNE       S0, V0, .o54
	NOP
	LW        S0, 148 (S2)
	.o54
	LW        A1, 0 (S1)
	JAL       ~Func:get_variable
	COPY      A0, S2
	COPY      S1, V0
	JAL       ~Func:get_actor
	COPY      A0, S0
	COPY      S0, V0
	%now...	S0 = target actor (pointer)
	%		S1 = turn duration
	%		S2 = script context
	%Increment the hustle drink timer
	LB		  T0, 20A (S0)
	ADDIU	  T0, T0, 1
	SB		  T0, 20A (S0)
	%Cool effects
	/%
	MFC1      A1, F26
	MFC1      A2, F24
    MFC1      A3, F20	
	%/
	LW		  A1, 144 (S0)
	LW		  A2, 148 (S0)
	LW		  A3, 14C (S0)
	LI        A0, 3			%subtype 3 = vanish?
	LI        V0, 1E		%duration? (30 -> 15)
	LIF		  F22, 1.4				%scale?
	SWC1      F22, 10 (SP)
	JAL       80070BB0
	SW        V0, 14 (SP)
	LW        V1, C (V0)
	ADDIU     A0, R0, FF                %R
	SB        A0, 68 (V1)
	LW        V1, C (V0)
    ADDIU     A0, R0, A0                %G
	SB        A0, 69 (V1)
	LW        V1, C (V0)
	ADDIU     A1, R0, 60                %B
	SB        A1, 6A (V1)
	LW        A0, C (V0)
	ADDIU     V1, R0, FF                %R 2
	SB        V1, 6B (A0)
	LW        A0, C (V0)
	ADDIU     V1, R0, D0                %G 2
	SB        V1, 6C (A0)
	LW        V1, C (V0)
    ADDIU     A1, R0, D0                %B 2
	SB        A1, 6D (V1)   	
	%Done with stuff
	LI		  V0, 2
	JPOP		S0, S1, S2, S3, RA
}

%same as above but no visual effect
#export:Function:Battle $HustleTargetNoEffects
{
	%copied the top of VanishActor
	PUSH		S0, S1, S2, S3, RA
	COPY      S2, A0
	LW        S1, C (S2)
	/%		??? what did this stuff do?
    BNEL      A1, R0, .o24
	SW        R0, 70 (S2)
    .o24
    LW        V0, 70 (S2)
	BNE       V0, R0, .oE0
	NOP
	%/
	LW        A1, 0 (S1)
	ADDIU     S1, S1, 4
	JAL       ~Func:get_variable
	COPY      A0, S2
	COPY      S0, V0
	LI        V0, FF81
	BNE       S0, V0, .o54
	NOP
	LW        S0, 148 (S2)
	.o54
	LW        A1, 0 (S1)
	JAL       ~Func:get_variable
	COPY      A0, S2
	COPY      S1, V0
	JAL       ~Func:get_actor
	COPY      A0, S0
	COPY      S0, V0
	%now...	S0 = target actor (pointer)
	%		S1 = turn duration
	%		S2 = script context
	%Increment the hustle drink timer
	LB		  T0, 20A (S0)
	ADDIU	  T0, T0, 1
	SB		  T0, 20A (S0)
	%Done with stuff
	LI		  V0, 2
	JPOP		S0, S1, S2, S3, RA
}




%Code for making the extra turns work

%Clear out some space
%20A + actor pointer appears unused
@Hook 193F84 %802656A4
{
SB		R0, 20A (S1)	%Now bonus turns
SB		R0, 21C (S1)	%status
J		802656AC
SW		V0, 20C (S1)	%status table? (I don't think there are any enemies that use a 00000000 status table though)
}

/%
175B8C --> 802472AC
Function_btl_state_update_next_enemy
%/
@Hook 175C64 %80247384
{
%Use actor pointer in S0 to check if additional turns are available
PUSH	T0, T1, T2
LB		T0, 20A (S0)
SLT		T1, R0, T0
ADDIU	A0, A0, 1
BEQ		T1, R0, .no
SB		R0, 20A (S0)
ADDI	A0, A0, FFFF	%undo the addition above
ADDI	T0, T0, FFFF
SB		T0, 20A (S0)
.no
POP		T0, T1, T2
J		8024738C
SW		S0, 174 (S1)	%store current enemy moving?
}


%Problem: above code is not run for the final enemy
%So I'll have to do it a different way

@Hook 175BD4 %802472F4
{
	%Major problem: I don't have an enemy pointer for the final enemy
	PUSH	RA, S0, S1, S2, S3, V1, A1
	%Check if this is the final enemy (do nothing if not)
	LB		A0, 170 (S1)		%next enemy
	LB		V0, 171 (S1)		%enemy count
	SLT		V0, A0, V0
	BNE		V0, R0, .end
	NOP
	%try to recreate the stuff used to find the enemy pointer
	ADDI	A0, A0, FFFF
	ORI		A1, A1, 4000
	SLL		V0, A0, 1
	ADDU	V0, S1, V0
	LH		V1, 140 (V0)
	LHU		V0, 140 (V0)
	BLTZ	V1, .end			%skip past if not a real enemy ID
	ANDI	V0, V0, FF
	SLL		V0, V0, 2
	ADDU	V0, S1, V0
	LW		S0, E0 (V0)
	%now check hustle turns
	LB		S2, 20A (S0)
	SLT		S3, R0, S2
	BEQ		S3, R0, .end
	SB		R0, 20A (S0)
	%decrement
	LB		A0, 170 (S1)		%next enemy
	ADDI	A0, A0, FFFF
	SB		A0, 170 (S1)		%next enemy
	%
	ADDI	S2, S2, FFFF
	SB		S2, 20A (S0)
	.end
	POP		RA, S0, S1, S2, S3, V1, A1
	LB		A0, 170 (S1)		%next enemy
	J		802472FC
	LB		V0, 171 (S1)		%enemy count
}