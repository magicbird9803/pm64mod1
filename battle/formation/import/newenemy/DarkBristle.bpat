

#define .AnimStop	00F20100
#define .AnimIdle	00F20101
#define .AnimOpenSpikes		00F20103
#define .AnimCloseSpikes	00F20105
#define .AnimSpin	00F20104
#define .AnimPain	00F20102
#define .AnimDizzy	00F20100
#define .AnimDead	00F20100

%Chapter 5 bristles
%5 atk, 5 hp, 6 def
%Your best bet is quakes
%Not a lot of other ways to get past that 6 def 

%give them chapter 5 ish stats
#new:Actor $DarkBristle
{
% stats
[Index]        67b
[Level]        18`b
[MaxHP]         5`b
[Coins]         2`b
[Flags]       00000000
[StatusTable] $StatusTable_DarkBristle 
% ai
[PartsCount]  1`s 
[PartsTable] $PartsTable_DarkBristle 
[Script]      $Script_Init_DarkBristle 
% move effectiveness
[Escape]       50`b 
[Item]         30`b 
[AirLift]      20`b 
[Hurricane]    20`b % Bow's "Spook" as well
[UpAndAway]    95`b 
[PowerBounce]  95`b 
[SpinSmash]     4`b % weight (0-4)
% ui positions
[Size]         48`b  59`b % width height - affects shadow
[HealthBar]     0`b   4`b % dx dy
[StatusTurn]  -10`b  20`b % dx dy
[StatusIcon]   10`b  20`b % dx dy
}

#new:StatusTable $StatusTable_DarkBristle
{
.Status:Normal            0`
.Status:Sleep             0`
.Status:Poison            0`
.Status:Frozen            0`
.Status:Dizzy            10`
.Status:Static            0`
.Status:Paralyze          0`
.Status:Shrink            0`
.Status:Stop             60`
.Status:SleepTurnMod      0`
.Status:PoisonTurnMod     0`
.Status:FrozenTurnMod     0`
.Status:DizzyTurnMod     -1`
.Status:StaticTurnMod     0`
.Status:ParalyzeTurnMod   0`
.Status:ShrinkTurnMod     0`
.Status:StopTurnMod      -1`
.Status:End
}

#new:PartsTable $PartsTable_DarkBristle
{
00800000 01000000 FE1900FF $IdleAnimations_DarkBristle $DefenseTable_DarkBristle 00000000 00000002 FDF60000 00000000 
}

#new:IdleAnimations $IdleAnimations_DarkBristle
{
.Status:Normal    .AnimIdle
.Status:Stone     .AnimStop
.Status:Sleep     .AnimIdle
.Status:Poison    .AnimIdle
.Status:Stop      .AnimStop
.Status:Static    .AnimIdle
.Status:Paralyze  .AnimStop
.Status:Dizzy     .AnimDizzy
.Status:End
}

#new:DefenseTable $DefenseTable_DarkBristle
{
.Element:Normal 00000006 %+2
.Element:Fire 	00000064 % immune
.Element:Blast  00000063 % also immune
.Element:Quake  FFFFFFFF % still weakness
.Element:End
}

#new:Script $Script_Init_DarkBristle
{
	Call	BindTakeTurn	( .Actor:Self $Script_TakeTurn_DarkBristle )
	%Call	BindIdle		( .Actor:Self $Script_Idle_DarkBristle )
	Call	BindHandleEvent	( .Actor:Self $Script_HandleEvent_DarkBristle )
	Call	GetActorPos		( .Actor:Self *Var[0] *Var[1] *Var[2] )
	%Call	SetActorVar		( .Actor:Self 00000000 .False )
	%Call	SetActorVar		( .Actor:Self 00000001 00000000 )
	%Sub  *Var[1]  2` % Mih is bad
	%Call	ForceHomePos	( .Actor:Self *Var[0] *Var[1] *Var[2] )
	Call	HPBarToHome		( .Actor:Self )
	% Can't jump, can't hammer!!
	Call     SetPartEventFlags 	( .Actor:Self 00000002 00010010 )
	Return
	End
}

#new:Script $Script_HandleEvent_DarkBristle
{
	Call     UseIdleAnimation 	( .Actor:Self .False )
	Call     EnableIdleScript 	( .Actor:Self 00000000 )
	Call     GetLastEvent 	( .Actor:Self *Var[0] )
	Switch  *Var[0] 
		CaseOR  ==  .Event:HitCombo
		CaseOR  ==  .Event:Hit
			SetConst  *Var[0]  00000001 
			SetConst  *Var[1]  .AnimPain 
			ExecWait DoNormalHit 
		EndCaseGroup
		Case  ==  .Event:BurnHit
			SetConst  *Var[0]  00000001 
			SetConst  *Var[1]  .AnimPain 
			SetConst  *Var[2]  .AnimDead 
			ExecWait DoBurnHit 
		Case  ==  .Event:BurnDeath
			SetConst  *Var[0]  00000001 
			SetConst  *Var[1]  .AnimPain 
			SetConst  *Var[2]  .AnimDead 
			ExecWait DoBurnHit 
			SetConst  *Var[0]  00000001 
			SetConst  *Var[1]  .AnimDead 
			ExecWait DoDeath 
			Return
		Case  ==  .Event:SpinSmashHit
			SetConst  *Var[0]  00000001 
			SetConst  *Var[1]  .AnimPain 
			ExecWait 8029B998 
		Case  ==  .Event:SpinSmashDeath
			SetConst  *Var[0]  00000001 
			SetConst  *Var[1]  .AnimPain 
			ExecWait 8029B998 
			SetConst  *Var[0]  00000001 
			SetConst  *Var[1]  .AnimPain 
			ExecWait DoDeath 
			Return
		Case  ==  .Event:ShockHit
			Call     PlaySoundAtActor 	( .Actor:Self 00000759 )
			SetConst  *Var[0]  00000001 
			SetConst  *Var[1]  .AnimPain 
			ExecWait DoShockHit 
			SetConst  *Var[0]  00000001 
			SetConst  *Var[1]  .AnimPain 
			ExecWait 8029BD70 
			ExecWait $Script_ReturnHome_DarkBristle 
		Case  ==  .Event:ShockDeath
			Call     PlaySoundAtActor 	( .Actor:Self 00000759 )
			SetConst  *Var[0]  00000001 
			SetConst  *Var[1]  .AnimPain 
			ExecWait DoShockHit 
			SetConst  *Var[0]  00000001 
			SetConst  *Var[1]  .AnimPain 
			ExecWait DoDeath 
			Return
		CaseOR  ==  00000017
		CaseOR  ==  .Event:Immune
		CaseOR  ==  .Event:AirLiftFailed
		CaseOR  ==  .Event:BurnTaunt
			SetConst  *Var[0]  00000001 
			SetConst  *Var[1]  .AnimIdle 
			ExecWait DoImmune 
		EndCaseGroup
		Case  ==  .Event:Death
			SetConst  *Var[0]  00000001 
			SetConst  *Var[1]  .AnimPain 
			ExecWait DoNormalHit 
			SetConst  *Var[0]  00000001 
			SetConst  *Var[1]  .AnimPain 
			ExecWait DoDeath 
			Return
		Case  ==  .Event:BeginFirstStrike
			Call     SetActorPos 	( .Actor:Self 00000014 00000000 00000000 )
			Call     HPBarToCurrent ( .Actor:Self )
		Case  ==  .Event:EndFirstStrike
			ExecWait $Script_ReturnHome_DarkBristle 
			Call     HPBarToHome 	( .Actor:Self )
		Case  ==  .Event:RecoverStatus
			SetConst  *Var[0]  00000001 
			SetConst  *Var[1]  .AnimIdle 
			ExecWait DoRecover 
		Case  ==  .Event:ScareAway
			SetConst  *Var[0]  00000001 
			SetConst  *Var[1]  .AnimSpin 
			SetConst  *Var[2]  .AnimPain 
			ExecWait DoScareAway 
			Return
		Case  ==  .Event:BeginAirLift
			SetConst  *Var[0]  00000001 
			SetConst  *Var[1]  .AnimSpin 
			ExecWait DoAirLift 
		Case  ==  .Event:BlowAway
			SetConst  *Var[0]  00000001 
			SetConst  *Var[1]  .AnimSpin 
			ExecWait DoBlowAway 
			Return
		Case  ==  .Event:SpikeTaunt
	      Call     GetStatusFlags ( .Actor:Self *Var[0] )
	      If  *Var[0]  !&  0035D000
	      Wait     6`
	      Call     SetAnimation 	( .Actor:Self 00000001 .AnimOpenSpikes )
	      EndIf
	      Wait     0000000A 
	      Call     GetActorPos 	( .Actor:Self *Var[0] *Var[1] *Var[2] )
	      Call     GetStatusFlags ( .Actor:Self *Var[3] )
	      If  *Var[3]  &  00080000 
	         Add  *Var[1]  0000000A 
	      Else
	         Add  *Var[1]  0000001A 
	      EndIf
	      Call     PlayEffect  	( ~FX:LensFlare:Rotating *Var[0] *Var[1] *Var[2] 00000014 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 )
	      Wait     00000009
	      Call     SetAnimation 	( .Actor:Self 00000001 .AnimCloseSpikes )
	      Wait     5` 
		Default
	EndSwitch
	Call     EnableIdleScript 	( .Actor:Self 00000001 )
	Call     UseIdleAnimation 	( .Actor:Self .True )
	Return
	End
}

#new:Script $Script_ReturnHome_DarkBristle
{
	% Return home
	Call     SetActorJumpGravity 	( .Actor:Self *Fixed[0.6] )
	Call     SetGoalToHome 	( .Actor:Self )
	Call     JumpToGoal  	( .Actor:Self 0000000F 00000000 00000001 00000002 )
	Call     8027D7F0 ( )
	Wait     12`
	Call     SetAnimation 	  ( .Actor:Self 00000001 .AnimCloseSpikes )
	Wait     8`
	Call     SetAnimation 	( .Actor:Self 00000001 .AnimIdle )
	Wait	 8` 
	Return
	End
}

#new:Script $Script_TakeTurn_DarkBristle
{
	% Go to player
	Call     UseIdleAnimation ( .Actor:Self .False )
	Call     EnableIdleScript ( .Actor:Self 00000000 )
	Call     SetTargetActor   ( .Actor:Self .Actor:Player )
	Call     SetGoalToTarget 	( .Actor:Self )
	Call     UseBattleCamPreset 	  ( 0000003F )
	Call     BattleCamTargetActor   ( .Actor:Self )
	%Call     8024ECF8 ( FFFFFFFF 00000001 00000000 )
	Wait     3`
	Call     PlaySoundAtActor   ( .Actor:Self 00002061)
	Call     SetAnimation 	  ( .Actor:Self 00000001 .AnimOpenSpikes )
	Wait     8`
	Call     PlaySoundAtActor 	( .Actor:Self 0000030F)
	Call     SetAnimation 	  ( .Actor:Self 00000001 .AnimSpin )
	Wait     12`
	Call     EnemyTestTarget ( .Actor:Self *Var[A] 00000000 00000000 00000001 00000010 )
	Switch  *Var[A] %miss
		CaseOR  ==  00000006 
		CaseOR  ==  00000005 
			Call     GetActorPos 	( .Actor:Player *Var[0] *Var[1] *Var[2] )
			Sub      *Var[0]  50`
			Call     SetGoalPos 	( .Actor:Self *Var[0] *Var[1] *Var[2] )
			Call     SetActorSpeed 	( .Actor:Self *Fixed[10.0] )
			Call     RunToGoal   	( .Actor:Self 00000000 .True )
			%Call     SetAnimation 	( .Actor:Self 00000001 .AnimIdle )
			Wait	 12`		%wait
			%Call     SetAnimation 	( .Actor:Self 00000001 .AnimSpin )
			Call     SetGoalToTarget 	( .Actor:Self )	
			If  *Var[A]  ==  00000005 
				Call     EnemyTestTarget ( .Actor:Self *Var[0] 80000000 00000000 00000000 00000000 )
			EndIf
			% Return home
			Call     SetActorJumpGravity 	( .Actor:Self *Fixed[0.6] )
			Call     SetGoalToHome 	( .Actor:Self )
			Call     AddActorDecoration 	( .Actor:Self 00000001 00000000 .Decoration:Sweat )
			Call     RunToGoal   	( .Actor:Self 00000000 .True )
			Call     RemoveActorDecoration 	( .Actor:Self 00000001 00000000 )
			Call     8027D7F0 ( )
			Wait     12`
			Call     SetAnimation 	  ( .Actor:Self 00000001 .AnimCloseSpikes )
			Wait     8`
			Call     SetAnimation 	( .Actor:Self 00000001 .AnimIdle )
			Wait	 8` 
			Call	 EnableIdleScript  ( .Actor:Self 00000001 )
			Call	 UseIdleAnimation  ( .Actor:Self .True )
			Return
		EndCaseGroup
	EndSwitch
	Call     GetActorPos 	( .Actor:Player *Var[0] *Var[1] *Var[2] )
	Add      *Var[0]  00000014 
	Call     SetGoalPos 	( .Actor:Self *Var[0] *Var[1] *Var[2] )
	Call     RandInt     	( 2` *Var[0] )
	If  *Var[0]  <  1`
		Call     SetActorSpeed 	( .Actor:Self *Fixed[16.0] )
	Else
		Call     SetActorSpeed 	( .Actor:Self *Fixed[8.0] )
	EndIf
	Call     RunToGoal   	( .Actor:Self 00000000 .True )
	Call     SetGoalToTarget 	( .Actor:Self )
	% Damage the sucker
	Call     EnemyDamageTarget 	( .Actor:Self *Var[0] 00000000 00000000 00000000 00000003 00000020 )
	Call     UseBattleCamPreset 	( 00000002 )
	Call     PlaySoundAtActor   ( .Actor:Self 0000030F)
	Wait     00000002
	% Return home
	Call     SetActorJumpGravity 	( .Actor:Self *Fixed[0.6] )
	Call     SetGoalToHome 	( .Actor:Self )
	Call     JumpToGoal  	( .Actor:Self 0000000F 00000000 00000001 00000002 )
	Call     8027D7F0 ( )
	Wait     12`
	Call     SetAnimation 	  ( .Actor:Self 00000001 .AnimCloseSpikes )
	Wait     8`
	Call     SetAnimation 	( .Actor:Self 00000001 .AnimIdle )
	Wait	 8` 
	Call	 EnableIdleScript  ( .Actor:Self 00000001 )
	Call	 UseIdleAnimation  ( .Actor:Self .True )
	Return
	End
}